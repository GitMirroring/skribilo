;;; Exercise the `biblio' routines.                  -*- Scheme -*-
;;;
;;; Copyright (C) 2022 Ludovic Court√®s <ludo@gnu.org>
;;;
;;; This file is part of Skribilo.
;;;
;;; Skribilo is free software; you can redistribute it and/or modify it
;;; under the terms of the GNU Lesser General Public License as published by
;;; the Free Software Foundation; either version 3 of the License, or (at
;;; your option) any later version.
;;;
;;; Skribilo is distributed in the hope that it will be useful, but
;;; WITHOUT ANY WARRANTY; without even the implied warranty of
;;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Lesser
;;; General Public License for more details.
;;;
;;; You should have received a copy of the GNU Lesser General Public License
;;; along with this program.  If not, see <http://www.gnu.org/licenses/>.

(define-module (tests biblio)
  #:use-module ((skribilo reader) #:select (*document-reader*))
  #:use-module (skribilo ast)
  #:use-module (skribilo biblio)
  #:use-module (skribilo biblio template)
  #:use-module (srfi srfi-64))

(define sbib
  '(article
    scheme:r5rs
    (title "The Revised5 Report on the Algorithmic Language Scheme")
    (author "Richard Kelsey, William D. Clinger, Jonathan Rees")
    (journal "Higher-Order and Symbolic Computation")
    (volume "11")
    (number "1")
    (month "Sep")
    (year "1998")
    (url "https://www.gnu.org/software/guile/manual/r5rs.html")))

(define table
  (make-bib-table 'table))


(test-begin "biblio")

(test-equal "parse-bib"
  "The Revised5 Report on the Algorithmic Language Scheme"
  (begin
    (call-with-input-string (object->string sbib)
      (lambda (port)
        (parameterize ((*document-reader* read))
          (parse-bib table port))))
    (and (bib-table? table)
         (let ((entry (resolve-bib table 'scheme:r5rs)))
           (and (is-markup? entry '&bib-entry)
                (markup-body (markup-option entry 'title)))))))

(test-equal "bibliography-template: simple"
  (let ((entry (resolve-bib table 'scheme:r5rs)))
    (list "A: " (markup-option entry 'author)
          "T: " (markup-option entry 'title)
          "Y: " (markup-option entry 'year)))
  (let ((template (bibliography-template "A: " author
                                         "T: " title
                                         "Y: " year)))
    (template (resolve-bib table 'scheme:r5rs))))

(test-equal "bibliography-template: conditionals"
  (let ((entry (resolve-bib table 'scheme:r5rs)))
    (list "A: " (markup-option entry 'author)
          "T: " (markup-option entry 'title)
          "Y: " (list (markup-option entry 'year))
          "B: " '("no")))
  (let ((template (bibliography-template "A: " (or editor author)
                                         "T: " (or booktitle title)
                                         "Y: " (if year year "N")
                                         "B: " (if booktitle "yes" "no"))))
    (template (resolve-bib table 'scheme:r5rs))))

(test-equal "bibliography-template: tricky things"
  (let ((entry (resolve-bib table 'scheme:r5rs)))
    (list "A: " (markup-option entry 'author)
          "Y: " (markup-option entry 'year) "!"))
  (let ((template (bibliography-template ("A: " author)
                                         ("T: " booktitle "+" title)
                                         ("Y: " year "!"))))
    (template (resolve-bib table 'scheme:r5rs))))

(test-end "biblio")
